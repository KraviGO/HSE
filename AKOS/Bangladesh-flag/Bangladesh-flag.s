# Рисование подобия флага Бангладеша
# В цикле осуществляется заполнение дисплея.
# Общая:
# r2 = r*r
# W = WIDTH*9/20  - центр окружности по горизонтали (смещен к древку)
# H = HIGHT/2     - центр окружности по вертикали
# dW = |W-X|	- расстояние от текущей точки до центра по горизонтали
# dH = |H-Y|	- расстояние от текущей точки до центра по вертикали
# dW2 = dW*dW
# dH2 = dH*dH
# l2 = dH2 + dH2 - расстояние до центра круга
# l2 < r2 - круг

# Выичление модуля числа
.macro abs(%reg)
	bgez %reg ok
	neg %reg %reg
ok:
.end_macro

# .include "macro-syscalls.m"
.eqv WIDTH          512
.eqv HIGHT          256
.eqv circleColor    0xF42A41	# Цвет круга
.eqv flagColor      0x006A4E	# Цвет флага
.eqv BASE           0x10010000	# Начало видеопамяти
  .text
main:
    li      s0 circleColor		# Хранение цвета круга
    li      s1 flagColor		# Хранение цвета флага
    li      s2 BASE				# Хранение начала видеопамяти
    li      t0 WIDTH			# Хранение ширины экрана (maxX)
    li      t1 HIGHT			# Хранение высоты экрана (maxY)
    # Вычисление центра круга
    # Центр смещен к древку: WIDTH * 9 / 20
    li      t4 9
    mul     t2 t0 t4    		# WIDTH * 9
    li      t4 20
    div     t2 t2 t4     		# (WIDTH * 9) / 20 - центр по горизонтали
    srli    t3 t1 1     		# Получение центра круга по вертикали (Н=maxY/2)
    # Вычисление радиуса (радиус = 1/5 высоты флага)
    li      t4 5
    div     a0 t1 t4     		# HIGHT / 5 - радиус круга
    mul     s3 a0 a0			# R2 = R*R квадрат радиуса не зависит от координат
    mv      t5 zero     		# Y = 0 - идем по строкам, реже обход по вертикали
hLoop:							# Цикл по вертикали
    mv      t6 zero     		# X = 0
    sub		s7 t3 t5			# Расстояние до центра по вертикали (dY=maxY/2-Y)
    #abs(s7)
    mul		s5 s7 s7			# Квадрат расстояния по вертикали (dY2=dY^2)
wLoop:							# Цикл по горизонтали
    sub		s8 t2 t6			# Расстояние до центра по горизонтали (dX=centerX-X)
    #abs(s8)
    mul		s6 s8 s8			# Квадрат расстояния по горизонтали
    add		s4 s5 s6			# Сумма квадратов - квадрат расстояния (dX2+dY2)
    # Проверки условий для раскраски
    blt     s3 s4 drawFlag		# Не в круге
    sw      s0 (s2)     		# Красим круг
    b       next
drawFlag:
    sw      s1 (s2)     		# Красим флаг
next:
    # Следующая точка и проверки на окончание
    addi    s2 s2 4     		# Следующий адрес в видеопамяти
    addi    t6 t6 1     		# X++
    blt     t6 t0 wLoop
    addi    t5 t5 1     		# Y++
    blt     t5 t1 hLoop
    
    li a7, 10
    ecall